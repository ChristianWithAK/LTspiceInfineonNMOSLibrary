* INFINEON Power Transistors                                    *
* LTPICE Library                                                *
*                                                               *
*                                                               *
* Version 040122                                                *
*                                                               *
*****************************************************************
*                                                               *
* The Simulation Model is subject to change without notice. In  *
* addition, models can be a useful tool in evaluating device    *
* performance, they cannot reflect the accurate device          *
* performance under all conditions, nor are they intended to    *
* replace bread boarding for final verification. Infineon       *
* therefore does not assume any warranty or liability           *
* whatsoever arising from their use. Infineon does not assume   *
* any warranty or liability for the values and functions of the *
* Simulation Model.                                             *
* The methods and results of the Simulation Model are to the    *
* best of our knowledge                                         *
* correct. However, the user is fully responsible to verify and *
* validate these results under the operating conditions and in  *
* the environment of its application. Infineon will not bear    *
* the responsibility arising out of or in connection with any   *
* malfunction of the Simulation Models.                         *
* Models provided by Infineon are not warranted by Infineon as  *
* completely and comprehensively representing all the           *
* specifications and operating characteristics of the           *
* semiconductor products to which these models relate. The      *
* models describe the characteristics of typical devices. In    *
* all cases, the current data sheet information for a given     *
* device is the conclusive design guideline and the only actual *
* performance specification.                                    *
*                                                               *
*                                                               *
* This library contains electrical models of the following      *
* Infineon transistors:                                         *
*                                                               *
*   IRF150P220          IRF150P221                              *
*                                                               *
*****************************************************************
* thermal nodes of level 3 models:                              *
*                                                               *
*  .SUBCKT IRF150P220 drain gate source Tj Tcase                *
*  Tj :    potential=temperature (in °C) at junction (typically *
*          not connected)                                       *
*  Tcase : node where the boundary condition - external heat    *
*          sinks etc - have to be connected (ideal heat sink    *
*          can be modeled by using a voltage source stating the *
*          ambient temperature in °C between Tcase and ground.  *
*                                                               *
*****************************************************************

.options Thev_Induc=1

.SUBCKT S5_150_f_var dd g s0 Tj PARAMS: a=1 dVth=0 dR=0 dgfs=0 Inn=1
+Unn=1 Rmax=1 gmin=1 Rs=1 Rp=1 dC=0 Rm=1u

.PARAM b0=15.26       p0=9.73        p1=-31.4m      p2=50u         mubet=987m     fbet=0
.PARAM Vth0=4.83      c=1.4          Fm=200m        Fn=500m        al=500m        auth=3.5m
.PARAM dvx=550m       dvgs=-200m     auth_sub=3.5m

.PARAM Rd=64.6m       rpara=500u     nmu=3.5        Rf=520m

.PARAM lnIsj=-25.46   ndi=1.14       Rdi=13.96m     nmu2=0         n_Isj=1        UB=161
.PARAM ab=65m         ab2=0          UT=100m        lB=-23         td=30n         ta=30n

.PARAM kbq=85.8u      Tref=298       T0=273

.PARAM f3=176p        f3a=51p

.PARAM ps1=8.32p      ps2=-774m      ps3=19.3p      ps4=-47.4m     ps5=0.636p     ps6=2.5p
.PARAM ps7=0          pc0=15p

.PARAM qs7=93.3p      qs8=-19.1m

.PARAM q80=53.1p      q81=49.7p      q82=0.61p      qs1=25.6p      qs4=67p        qs5=-413m

.PARAM x1={(q80-q81)/q82}       x2={q80/q82}
.PARAM Vmin=4.03      Vmax=5.63      dCmax=330m
.PARAM Vth={Vth0+(Vmax-Vth0)*limit(dVth,0,1)-(Vmin-Vth0)*limit(dVth,-1,0)}
.PARAM q0={b0*((1-fbet)*(T0/Tref)**mubet+fbet)*a}
.PARAM q1={(Unn-Inn*Rs-Vth0)*q0}
.PARAM q2={(Fm*SQRT(0.4)-c)*Inn*q0}
.PARAM Rlim={((q1+2*q2*Rmax-SQRT(q1**2+4*q2))/(2*q2))/(1+rpara*(Inn/a)**2)}
.PARAM dRd={Rd/a+if(dVth==0,limit(dR,0,1)*max(Rlim-Rd/a-Rs-Rp,0),0)}
.PARAM bm={c/((1/gmin-Rs)**2*Inn*a*((1-fbet)*(T0/Tref)**mubet+fbet))}
.PARAM bet={b0+(b0-bm)*if(dR==0,if(dVth==0,limit(dgfs,-1,0),0),0)}
.PARAM dC1={1+dCmax*limit(dC,0,1)}
.PARAM dC2={1+dCmax*limit(dC,0,1)}

.PARAM Cgs0={f3*a*dC1}
.PARAM Cgs1={f3a*a*dC1}

.PARAM Cds2={qs7*a*dC1}
.PARAM Cds5={qs1*a*dC1}
.PARAM Cds7={qs4*a*dC1}
.PARAM Cds8={q80*a*dC1}

.PARAM Cdg1={(ps1*a+pc0*sqrt(a))*dC2}
.PARAM Cdg2={ps3*a*dC2}
.PARAM Cdg3={(ps5*a+ps6)*dC2}
.PARAM dRdi={Rdi/a}

.FUNC Ue(g,y,w)  {(g-Vth+auth*(w-Tref)+Fm*y**Fn)}
.FUNC Ue1(g,y,w) {Ue(g,y,w)+(1+limit(Ue(g,y,w)+dvx,0,1)**2*(2*limit(Ue(g,y,w)+dvx,0,1)-3))*(dvgs+(auth_sub-auth)*(w-Tref))}

.FUNC I0(Uee,p,pp,z1) {if(Uee>pp,(Uee-c*z1)*z1,p*(pp-p)/c*exp((Uee-pp)/p))}
.FUNC Ih(Uds,T,p,Uee) {bet*((1-fbet)*(T0/T)**mubet+fbet)*I0(Uee,p,min(2*p,p+c*Uds),min(Uds,Uee/(2*c)))}
.FUNC Jh(d,g,w,y,s,x) {a*((Ih(s*y+min(d,0),w,(p0+(p1+p2*w)*w)*kbq*w,Ue1(g,y,w))+exp(min(lB+(d-UB-ab*(w-Tref))/UT,24))))}

.FUNC Idiode(Usd,Tj,Iss) {exp(min(ln(Iss)+Usd/(ndi*kbq*Tj),7))-Iss}
.FUNC Idiod(Usd,Tj)      {a*Idiode(Usd,Tj,exp(min(lnIsj+(Tj/Tref-1)*1.12/(ndi*kbq*Tj),7))*(Tj/Tref)**n_Isj)}

.FUNC Pr(Vss0)       {Vss0*Vss0/Rm}
.FUNC Q01(Uds) {a*(limit(Uds,x1,x2)*(q80-q82/2*limit(Uds,x1,x2))+min(Uds-x1,0)*q81-max(x1,0)*(q80-q81)/2)}

C_Cgs  g      s      {Cgs0}
C_Cgs1 g      s      {Cgs1}

E_Eds2 d      edep2  VALUE {V(d,s)-I(V_sense3)/Cds2}
C_Cds2 edep2  s      {Cds2}

C_Cds5 d      s      {Cds5}
E_Eds7 d      edep7  VALUE {if(qs5==0,0,V(d,s)-(exp(qs5*max(V(d,s),0))-1)/min(qs5,-1u)-min(V(d,s),0))}
C_Cds7 edep7  s      {Cds7}
E_Eds8 d      edep8  VALUE {V(d,s)-Q01(V(d,s))/Cds8}
C_Cds8 edep8  s      {Cds8}

E_Edg1 d    ox1 VALUE {if(ps2==0,0,V(d,g)-(exp(ps2*max(V(d,g),0))-1)/min(ps2,-1u)-min(V(d,g),0))}
C_Cdg1 ox1  g   {Cdg1}
E_Edg2 d    ox2 VALUE =
+{if(ps4==0,0,V(d,g)-((exp(ps4*(max(V(d,g)+ps7,0)))-exp(ps4*max(ps7,0)))/min(ps4,-1u)+min(V(d,g)+max(ps7,0),max(0,-ps7))))}
C_Cdg2 ox2  g   {Cdg2}
C_Cdg3 d    g   {Cdg3}



G_chan   d5a   s  VALUE={Jh(V(d5a,s),V(g,s),T0+limit(V(Tj),-200,300),(SQRT(1+4*al*max(V(d5a,s),0))-1)/2/al,sgn(V(d5a,s)),0)}
Rd06     d5a  d5  1u
V_sm     d    d5  0
G_RMos   d1    d  VALUE={V(d1,d)/(Rf*dRd+(1-Rf)*dRd*((limit(V(Tj),-200,999)+T0)/Tref)**nmu)/(1+rpara*(I(V_sense)/a)**2)}
V_sense  dd   d1  0
G_diode   s   d3  VALUE={Idiod(V(s,d3),T0+limit(V(Tj),-200,499))}
G_Rdio   d2   d1  VALUE={V(d2,d1)/(dRdi*((limit(V(Tj),-200,999)+T0)/Tref)**nmu2)}
V_sense2 d2   d3  0


L_L001 a c {td/(ta+td)}
R_R001 a b {1/ta}
V_sense3 c 0 0
E_E001 b 0 VALUE {I(V_sense2)}
E_E002 e 0 VALUE {Cds2*((exp(qs8*max(V(d,s),-1))-1)/min(qs8,-1u)-min(V(d,s)+1,0)*exp(-qs8))}
R_R002 e c 1
R_R003 a 0 500Meg

R1        g    s  1G
Rd01      d    s  500Meg
Rd02     d2    s  500Meg
Rd03     d1    d  1k


Rmet      s    s0 {Rm}


G_TH      0   Tj  VALUE =
+{(I(V_sense)-I(V_sense2))*V(d1,d)+I(V_sm)*V(d,s)+I(V_sense2)*V(d1,s)+Pr(V(s,s0))}


.ENDS

**********

.SUBCKT IRF150P220_L1 drain gate source PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 Ls=6n Ld=4n Lg=5n

.PARAM Rs=736u      Rg=0.5       Rd=91u       Rm=434u
.PARAM Inn=100      Unn=10       Rmax=2.7m    gmin=100
.PARAM act=53      

X1  d1 g s Tj1 S5_150_f_var PARAMS: a={act} dVth={dVth} dR={dRdson} dgfs={dgfs} Inn={Inn} Unn={Unn}
+Rmax={Rmax} gmin={gmin} Rs={Rs} Rp={Rd} dC={dC} Rm={Rm}

Rg    g1     g    {Rg}
Lg    gate   g1   {Lg*if(dgfs==99,0,1)}
Gs    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rm)}
Rsa   s1     s    1Meg
Ls    source s1   {Ls*if(dgfs==99,0,1)}
Rda   d1     d2   {Rd}
Ld    drain  d2   {Ld*if(dgfs==99,0,1)}

E1    Tj     w    VALUE={TEMP}
Vp    Tj1    Tj   0
R1    Tj     Tj1  1u
G_power 0    Tj   VALUE={V(s1,s)*V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rm)+V(g,g1)*V(g,g1)/Rg+V(d1,d2)*V(d1,d2)/Rd+I(Vp)}
R2      w    0    1u

.ENDS

**********

.SUBCKT IRF150P221_L1 drain gate source PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 Ls=6n Ld=4n Lg=5n

.PARAM Rs=539u      Rg=1.1       Rd=91u       Rm=237u
.PARAM Inn=100      Unn=10       Rmax=4.5m    gmin=75
.PARAM act=26.44   

X1  d1 g s Tj1 S5_150_f_var PARAMS: a={act} dVth={dVth} dR={dRdson} dgfs={dgfs} Inn={Inn} Unn={Unn}
+Rmax={Rmax} gmin={gmin} Rs={Rs} Rp={Rd} dC={dC} Rm={Rm}

Rg    g1     g    {Rg}
Lg    gate   g1   {Lg*if(dgfs==99,0,1)}
Gs    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rm)}
Rsa   s1     s    1Meg
Ls    source s1   {Ls*if(dgfs==99,0,1)}
Rda   d1     d2   {Rd}
Ld    drain  d2   {Ld*if(dgfs==99,0,1)}

E1    Tj     w    VALUE={TEMP}
Vp    Tj1    Tj   0
R1    Tj     Tj1  1u
G_power 0    Tj   VALUE={V(s1,s)*V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rm)+V(g,g1)*V(g,g1)/Rg+V(d1,d2)*V(d1,d2)/Rd+I(Vp)}
R2      w    0    1u

.ENDS

**********

.SUBCKT IRF150P220 drain gate source Tj Tcase PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 Zthtype=0 Ls=6n Ld=4n Lg=5n

.PARAM Rs=736u      Rg=0.5       Rd=91u       Rm=434u

.PARAM Inn=100      Unn=10       Rmax=2.7m    gmin=100
.PARAM RRf=438m     Rrbond=8m    Rtb=4.3      g2=662m
.PARAM act=53      

.FUNC   Pb(I,dT,Rb)  {Rb/(2*Rtb)*(I-limit(dT/(max(I,1n)*Rb)+RRf*I*g2,0,I))**2}

X1  d1 g s Tj S5_150_f_var PARAMS: a={act} dVth={dVth} dR={dRdson} dgfs={dgfs} Inn={Inn} Unn={Unn}
                                        +Rmax={Rmax} gmin={gmin} Rs={Rs} Rp={Rd} dC={dC} Rm={Rm}
Rg    g1     g    {Rg}
Lg    gate   g1   {Lg*if(dgfs==99,0,1)}
Gs    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rm)}
Rsa   s1     s    1Meg
Ls    source s1   {Ls*if(dgfs==99,0,1)}
Rda   d1     d2   {Rd}
Ld    drain  d2   {Ld*if(dgfs==99,0,1)}
Rsb   source s1    10
Rga   gate   g1    10
Rdb   drain  d2    10

G_TH  0    Tb  VALUE =  {Pb(abs(I(Ls)),V(Tj,Tcase),Rrbond*(1+(limit((V(Tb)+V(Tj))/2,-200,999)-25)*4m))}
Cthb  Tb      0               9.36m
Rthb  Tb      Tj              {Rtb}
Rth1  Tj      t1              {6.53m+limit(Zthtype,0,1)*2.41m}
Rth2  t1      t2              {52.84m+limit(Zthtype,0,1)*3.81m}
Rth3  t2      t3              {71.78m+limit(Zthtype,0,1)*3.7u}
Rth4  t3      t4              {25.6m+limit(Zthtype,0,1)*45.36m}
Rth5  t4      Tcase           {22.25m+limit(Zthtype,0,1)*39.42m}
Cth1  Tj      0               11.24m
Cth2  t1      0               7.373m
Cth3  t2      0               0.1292
Cth4  t3      0               1.09935
Cth5  t4      0               1.864m
Cth6  Tcase   0               2

.ENDS

**********

.SUBCKT IRF150P221 drain gate source Tj Tcase PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 Zthtype=0 Ls=6n Ld=4n Lg=5n

.PARAM Rs=539u      Rg=1.1       Rd=91u       Rm=237u

.PARAM Inn=100      Unn=10       Rmax=4.5m    gmin=75
.PARAM RRf=438m     Rrbond=8m    Rtb=4.3      g2=662m
.PARAM act=26.44   

.FUNC   Pb(I,dT,Rb)  {Rb/(2*Rtb)*(I-limit(dT/(max(I,1n)*Rb)+RRf*I*g2,0,I))**2}

X1  d1 g s Tj S5_150_f_var PARAMS: a={act} dVth={dVth} dR={dRdson} dgfs={dgfs} Inn={Inn} Unn={Unn}
                                        +Rmax={Rmax} gmin={gmin} Rs={Rs} Rp={Rd} dC={dC} Rm={Rm}
Rg    g1     g    {Rg}
Lg    gate   g1   {Lg*if(dgfs==99,0,1)}
Gs    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rm)}
Rsa   s1     s    1Meg
Ls    source s1   {Ls*if(dgfs==99,0,1)}
Rda   d1     d2   {Rd}
Ld    drain  d2   {Ld*if(dgfs==99,0,1)}
Rsb   source s1    10
Rga   gate   g1    10
Rdb   drain  d2    10

G_TH  0    Tb  VALUE =  {Pb(abs(I(Ls)),V(Tj,Tcase),Rrbond*(1+(limit((V(Tb)+V(Tj))/2,-200,999)-25)*4m))}
Cthb  Tb      0               9.36m
Rthb  Tb      Tj              {Rtb}
Rth1  Tj      t1              {11.05m+limit(Zthtype,0,1)*4.09m}
Rth2  t1      t2              {50.81m+limit(Zthtype,0,1)*8.4m}
Rth3  t2      t3              {43.67m+limit(Zthtype,0,1)*680n}
Rth4  t3      t4              {143.3m+limit(Zthtype,0,1)*45.67m}
Rth5  t4      Tcase           {100.86m+limit(Zthtype,0,1)*32.15m}
Cth1  Tj      0               2.95m
Cth2  t1      0               3.93m
Cth3  t2      0               543.688u
Cth4  t3      0               25.67m
Cth5  t4      0               243.898m
Cth6  Tcase   0               2

.ENDS

**********

.SUBCKT IRF150P220_L0 drain gate source

Lg     gate   g1   5n
Ld     drain  d1   4n
Ls     source s1   6n
Rs     s1     s2   736u  TC=3m

Rg     g1     g2   0.5

M1     d2     g2    s2    s2    DMOS    L=1u   W=1u
.MODEL DMOS NMOS ( KP=662.5  VTO=5  THETA=0  VMAX=1.5e5  ETA=0.005  LEVEL=3)
Rd     d1    d2    1.32m  TC=9m

Dbd     s2    d2    Dbt
.MODEL     Dbt    D(BV=161  M=0.65  CJO=4.93n  VJ=2.5V)
Rsp     s2    s3     1u
Dbd1    s3    d2     Dbt1
.MODEL     Dbt1    D(BV=1000   M=0.65  CJO=19.06n  VJ=2.5V)
Dbody   s2   21    DBODY
.MODEL DBODY  D(IS=21.2p  N=1.05  RS=0.01u  EG=1.12  TT=30n)
Rdiode  d1  21    0.23m  TC=1m

.MODEL   sw    NMOS(VTO=0  KP=10   LEVEL=1)
Maux      g2   c    a    a   sw
Maux2     b    d    g2    g2   sw
Eaux      c    a    d2    g2   1
Eaux2     d    g2   d2    g2   -1
Cox       b    d2   1.75n
.MODEL     DGD    D(M=0.42  CJO=1.75n  VJ=0.5)
Rpar      b    d2   1Meg
Dgd       a    d2   DGD
Rpar2     d2   a    10Meg
Cgs     g2    s2    12.03n

.ENDS IRF150P220_L0

*****************

.SUBCKT IRF150P221_L0 drain gate source

Lg     gate   g1   5n
Ld     drain  d1   4n
Ls     source s1   6n
Rs     s1     s2   539u  TC=3m

Rg     g1     g2   1.1

M1     d2     g2    s2    s2    DMOS    L=1u   W=1u
.MODEL DMOS NMOS ( KP=330.5  VTO=5  THETA=0  VMAX=1.5e5  ETA=0.005  LEVEL=3)
Rd     d1    d2    2.55m  TC=9m

Dbd     s2    d2    Dbt
.MODEL     Dbt    D(BV=161  M=0.65  CJO=2.46n  VJ=2.5V)
Rsp     s2    s3     1u
Dbd1    s3    d2     Dbt1
.MODEL     Dbt1    D(BV=1000   M=0.65  CJO=9.61n  VJ=2.5V)
Dbody   s2   21    DBODY
.MODEL DBODY  D(IS=10.6p  N=1.05  RS=0.02u  EG=1.12  TT=30n)
Rdiode  d1  21    0.45m  TC=1m

.MODEL   sw    NMOS(VTO=0  KP=10   LEVEL=1)
Maux      g2   c    a    a   sw
Maux2     b    d    g2    g2   sw
Eaux      c    a    d2    g2   1
Eaux2     d    g2   d2    g2   -1
Cox       b    d2   0.87n
.MODEL     DGD    D(M=0.42  CJO=0.87n  VJ=0.5)
Rpar      b    d2   1Meg
Dgd       a    d2   DGD
Rpar2     d2   a    10Meg
Cgs     g2    s2    6n

.ENDS IRF150P221_L0

*****************


