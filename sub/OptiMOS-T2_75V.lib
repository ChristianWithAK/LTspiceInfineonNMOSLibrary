* ------------------------------------------------------------------------------
* 
* DISCLAIMER
* 
* INFINEON'S MODEL TERMS OF USE
* 
* BY DOWNLOADING AND/OR USING THIS INFINEON MODEL ("MODEL"), THE USER
* (INCLUDING YOU) AGREES TO BE BOUND BY THE TERMS OF USE HERE STATED. IF USER
* DOES NOT AGREE TO ALL TERMS OF USE HERE STATED, USER SHALL NOT DOWNLOAD,
* USE OR COPY THE MODEL BUT IMMEDIATELY DELETE IT (TO THE EXTENT THAT IT
* WAS DOWNLOADED ALREADY).
* 
* 1. SCOPE OF USE
* 1.1 Any use of this Model provided by Infineon Technologies AG is subject
*     to these Terms of Use.
* 1.2 This Model, provided by Infineon, does not fully represent all of the
*     specifications and operating characteristics of the product to which
*     this Model relates.
* 1.3 This Model only describes the characteristics of a typical product.
*     In all cases, the current data sheet information for a given product
*     is the final design guideline and the only actual performance
*     specification. Although this Model can be a useful tool to evaluate
*     the product performance, it cannot simulate the exact product performance
*     under all conditions and it is also not intended to replace
*     bread-boarding for final verification.
* 
* 2. IMPORTANT NOTICE
* 2.1 Infineon Technologies AG ("Infineon") is not and cannot be aware of the
*     specific application of the Infineon's Model by User. However, Model may
*     from time to time be used by User in potentially harmful and/or life-
*     endangering applications such as traffic, logistic, medical, nuclear
*     or military applications or in other applications where failure of the
*     Model may predictably cause damage to persons' life or health or to
*     property (hereinafter "Critical Applications").
* 2.2 User acknowledges that Infineon has not specifically designed or
*     qualified the Model for Critical Applications that the Model may contain
*     errors and bugs and that User is required to qualify the Model for
*     Critical Applications pursuant to the applicable local quality, safety
*     and legal requirements before permitting or giving access to any such use.
* 
* 3. CONFIDENTIAL INFORMATION
*     User shall treat ideas, concepts and information incorporated in the
*     Model, the documentation and the content of this Terms of Use (together
*     hereinafter "Confidential Information") confidential, not disclose it to
*     any third party unless otherwise agreed in writing between User and
*     Infineon, not use it for any other purposes than using the Model for
*     simulation and testing purposes only.
* 
* 4. WARRANTY
* 4.1 User acknowledges that the Model is provided by Infineon under this Terms
*     of Use is provided free of charge and "AS IS" without any warranty or
*     liability of any kind and Infineon hereby expressly disclaims any
*     warranties or representations, whether express, implied, statutory or
*     otherwise, including but not limited to warranties of workmanship,
*     merchantability, fitness for a particular purpose, defects in the
*     Model, or non-infringement of third parties intellectual property rights.
* 4.2 Infineon reserves the right to make corrections, deletions, modifications,
*     enhancements, improvements and other changes to the Model at any time
*     or to move or discontinue any Model without notice.
* 
* 5. LIABILITY
* 5.1 Nothing in this Terms of Use shall limit or exclude Infineon's liability
*     under mandatory liability laws, for injuries to life, body or health,
*     for fraudulent concealment of defects in the software, or in cases
*     of Infineon's intentional misconduct or gross negligence.
* 5.2 Without prejudice to Sections 5.1, in cases of Infineon's slight
*     negligent breach of obligations that restrict essential rights or duties
*     arising from the nature of this Terms of Use in a way that there is a
*     risk of non-achievement of the purpose of this Terms of Use or of an
*     obligation whose observance User regularly may trust in and whereas
*     compliance with only makes proper execution of this Terms of Use
*     possible, Infineon's liability shall be limited to the typically,
*     foreseeable damage.
* 5.3 Without prejudice to Sections 8.1 and 8.2, Infineon's liability under
*     this Agreement shall be excluded in all other cases.
* 
* 6. EXPORT REGULATIONS
*     The User shall comply with all applicable national and international
*     laws and regulations, in particular the applicable export control
*     regulations and sanction programs. The User also agrees not to
*     export, re-export or transfer any software or technology developed
*     with or using information, software or technology offered by Infineon,
*     in violation of any applicable laws or regulations of the competent
*     authorities. Further, the User shall neither use any products,
*     information, software and technology offered by Infineon in or in
*     connection with nuclear technology or weapons of mass destruction
*     (nuclear, biological or chemical) and carriers thereof nor supply
*     military consignees.
* 
* 7. TERMINATION OF USE PERMIT
*     If the User violates these Terms of Use, such User's permit to use
*     this Model terminates automatically. In addition, Infineon may
*     terminate the User's permit to use this Model at its discretion and
*     at any time regardless of any violation of these Terms of Use. In
*     any of the foregoing events, the User is obliged to immediately destroy
*     any content that has been downloaded or printed from Infineon's website.
* 
* 8. MISCELLANEOUS
* 8.1 These Terms of Use are subject to the laws of the Federal Republic
*     of Germany with the exception of the United Nations on Purchase
*     Contracts on the International Sale of Goods dated April 11, 1980 (CISG).
*     The exclusive place of jurisdiction is Munich, Germany.
* 8.2 Should any provision in these Terms of Use be or become invalid, the
*     validity of all other provisions or agreements shall remain unaffected
*     thereby.
* 
* ------------------------------------------------------------------------------
* 
* Title:    INFINEON Power Transistors Simulation Models for PSpice
* Description: n-channel Transistors
*                 OptiMOS4 (OptiMOS-T2) 75V
* Authors:  Dr. Elmar Gondro, Tel: +49/89/234-29391
*           Günther Moyses,   Tel: +49/89/234-24811
*           Gerhard Nöbauer,  Tel: +43/(5)/1777-6653
* Email:    Elmar.Gondro@Infineon.com
*           Guenther.Moyses@Infineon.com
*           Gerhard.Noebauer@Infineon.com
* Support:  Simulate@Infineon.com
*
* ------------------------------------------------------------------------------
* 
* This library contains models of the following INFINEON transistors:
* 
*   NL
*    IPB80N07S4-05      IPP80N07S4-05      IPI80N07S4-05      
* 
* ------------------------------------------------------------------------------
* 
* Thermal nodes of level 3 models: 
* 
*  Tj:    Potential (temperature in °C) at junction (typically not connected) 
*  Tcase: Node where boundary condition (external heat sinks) must be connected.
*         Ideal heat sink can be modeled by using a voltage source stating the 
*         ambient temperature in °C between Tcase and ground. 
* 
* ------------------------------------------------------------------------------
* 
* This file also contains simplified models that are compatible to standard 
* Spice (Level 0) 
* 
* ------------------------------------------------------------------------------
* 
* History: 
* DD/MM/YY 
* 21/08/13 initial version                     (Gondro, Moyses)
*          technology models based on 
*          - Infineon_-_Simulation_Model_-_PSpice_-_OptiMOS__75V/OptiMOS3_75V.lib
*          - Infineon_-_Simulation_Model_-_PSpice_-_OptiMOS__-_80V/OptiMOS3_80V.lib
* 21/08/13 new NL models for:                                   (Gondro, Moyses)
*            IPB180N08S4-02
*            IPB160N08S4-03
*            IPB140N08S4-04
*            IPB120N08S4-03
*            IPP120N08S4-03
*            IPI120N08S4-03
*            IPB120N08S4-04
*            IPP120N08S4-04
*            IPI120N08S4-04
*            IPB80N08S4-06
*            IPP80N08S4-06
*            IPI80N08S4-06
*            IPD90N08S4-05
*            IPD50N08S4-13
*            IPG20N08S4-22
*            IPB80N07S4-05
*            IPP80N07S4-05
*            IPI80N07S4-05
* 16/10/13 new NL models for:                                   (Gondro, Moyses)
*            IIPC64S4N08
* 13/07/16 new NL model for:                                    (Gondro, Moyses)
*          - IIPC63S4N08
*          replaced Zthtype by dZth
* 10/05/17 Vth increased by 0.2V to center on target            (Gondro, Moyses)
*            => increased Vplateau requested by Valeo
* 22/06/17 split of voltage classes                                     (Gondro)
* 03/05/18 param Rlim limited to avoid negative sqrt                    (Gondro)
* 25/01/19 fixed bug in .FUNC Pb(I,dT,Rb)                               (Gondro)
* 
* ------------------------------------------------------------------------------
* 
* S3_75_g_var: SFET3-75V NL
* 
* ------------------------------------------------------------------------------


********************************************************************************
* Technology Models
********************************************************************************

.SUBCKT S3_75_g_var dd g s0 Tj PARAMS: a=1 dVth=0 dR=0 dgfs=0 Inn=1 Unn=1 Rmax=1
+ gmin=1 Rs=1 Rp=1 dC=0 Rm=1u heat=0

.PARAM  Fm=0.15      Fn=0.5      kbq=86.2u
.PARAM  c=1.594      muc=0.0     Vth0=4.275   auth=4m      al=0.5
.PARAM  UT=100m      ab=70m      ab2=0        lB=-23       UB=88

.PARAM  b0=33.46     p0=8.6935   p1=-33.5m    p2=56.5u

.PARAM  Rd=23.4m     nmu=3       Tref=298     T0=273       lnIsj=-25.89
.PARAM  ndi=1.155    Rdi=8m      nmu2=0       ta=20n       td=100n
.PARAM  Rf=0.58      nmu3=1.5    rpa=0

.PARAM  f3=390p      f3a=0p       
.PARAM  ps1=35p      ps2=-66.7m  ps3=90p      ps4=-2        ps5=1.18p     ps6=3.14p
.PARAM  qs1=43p      qs2=49p     qs3=-1       qs4=186p      qs5=-0.04 

.PARAM  Vmin=3.475   Vmax=4.975  dCmax=0.33
.PARAM  Vth={Vth0+(Vmax-Vth0)*limit(dVth,0,1)-(Vmin-Vth0)*limit(dVth,-1,0)}
.PARAM  q0={b0*((T0/Tref)**nmu3)*a}
.PARAM  q1={(Unn-Inn*Rs-Vth0)*q0}
.PARAM  q2={(Fm*sqrt(0.4)-c)*Inn*q0}
.PARAM  Rlim={(q1+2*q2*Rmax-sqrt(max(q1**2+4*q2,0)))/(2*q2)}
.PARAM  dRd={Rd/a+if(dVth==0,limit(dR,0,1)*max(Rlim-Rd/a-Rs-Rp,0),0)}
.PARAM  bm={c/((1/gmin-Rs)**2*Inn*a*(T0/Tref)**nmu3)}
.PARAM  bet={b0+(b0-bm)*if(dR==0,if(dVth==0,limit(dgfs,-1,0),0),0)}
.PARAM  dC1={1+dCmax*limit(dC,0,1)}

.PARAM  Cox1={ps1*a*dC1}
.PARAM  Cox2={ps3*a*dC1}
.PARAM  Cox3={(ps5*a+ps6)*dC1}
.PARAM  Cds0={qs1*a*dC1}
.PARAM  Cds1={qs2*a*dC1}
.PARAM  Cds2={qs4*a*dC1}
.PARAM  Cgs0={(f3a+f3*a)*dC1}
.PARAM  dRdi={Rdi/a}

.FUNC  I0(Uee,p,pp,z1,cc)  {if(Uee>pp,(Uee-cc*z1)*z1,p*(pp-p)/cc*exp((Uee-pp)/p))}
.FUNC  Ig(Uds,T,p,Uee,cc)  {bet*(T0/T)**nmu3*I0(Uee,p,min(2*p,p+cc*Uds),min(Uds,Uee/(2*cc)),cc)}
.FUNC  J(d,g,T,da,s) 
+ {a*s*((Ig(da,T,(p0+(p1+p2*T)*T)*kbq*T,g-Vth+auth*(T-Tref)+Fm*da**Fn,c*(T/Tref)**muc)+
+ exp(min(lB+(d-UB-ab*(T-Tref))/UT,25))))}

.FUNC  Idiode(Usd,Tj,Iss) {exp(min(log(Iss)+Usd/(ndi*kbq*Tj),7))-Iss}
.FUNC  Idiod(Usd,Tj)      {a*Idiode(Usd,Tj,exp(min(lnIsj+(Tj/Tref-1)*1.12/(ndi*kbq*Tj),7))*(Tj/Tref)**1)}

E_Edg1    d   ox  VALUE {if(V(d,g)>0,V(d,g)-(exp(ps2*max(V(d,g),0))-1)/ps2,0)}
C_Cdg1   ox   g   {Cox1}
E_Edg2    d   ox1 VALUE {if(V(d,g)>0,V(d,g)-(exp(ps4*max(V(d,g),0))-1)/ps4,0)}
C_Cdg2  ox1   g   {Cox2}
Vx        d   ox2 0
C_Cdg3  ox2   g   {Cox3}

E_Eds     d edep  VALUE {(V(d,s)-I(V_sense3)/(Cds0+Cds1+Cds2))}
C_Cds  edep    s  {Cds0+Cds1+Cds2}

C_Cgs   g    s   {Cgs0}

G_chan    d    s  VALUE={J(V(d,s),V(g,s),T0+limit(V(Tj),-200,500),(sqrt(1+4*al*abs(V(d,s)))-1)/2/al,sgn(V(d,s)))}
G_RMos   d1    d  VALUE={V(d1,d)/(Rf*dRd+(1-Rf)*dRd*((limit(V(Tj),-200,999)+T0)/Tref)**nmu)/(1+rpa*(I(V_sense)/a)**2)}
V_sense  dd   d1  0
G_diode   s   d3  VALUE={Idiod(V(s,d3),T0+limit(V(Tj),-200,499))}
G_Rdio   d2   d1  VALUE={V(d2,d1)/(dRdi*((limit(V(Tj),-200,999)+T0)/Tref)**nmu2)}
V_sense2 d2   d3  0

L_L001    a    c  {td/(ta+td)}
R_R001    a    b  {1/ta}
V_sense3  c    0  0
E_E001    b    0  VALUE {I(V_sense2)}
E_E002    e    0  VALUE {Cds1/qs3*(exp(qs3*max(V(d1,s),-1))-1)+Cds2/qs5*(exp(qs5*max(V(d1,s),-1))-1)+Cds0*V(d1,s)} 

R_R002    e    c  1
R_R003    a    0  500Meg

R1        g    s  1G
Rd01      d    s  500Meg
Rd02     d2    s  500Meg
Rd03     d1    d  1k

Rmet      s    s0 {Rm}

G_TH      0   Tj  VALUE =
+ {(limit(I(V_sense2)*V(d1,s)+(V(s,s0)**2)/Rm+(I(V_sense)-I(V_sense2))*V(d1,d)+
+ (I(V_sense)-I(V_sense2)-I(E_Edg1)-I(E_Edg2)-I(Vx)-I(E_Eds))*V(d,s),-10k,100k))}

.ENDS



********************************************************************************
* Level 3 Models
********************************************************************************

.SUBCKT IPB80N07S4_05 drain gate source Tj Tcase PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 dZth=0 Ls=2n Ld=1n Lg=4n

.PARAM Rs=1.13m     Rg=2.2       Rd=50u       Rm=465u
.PARAM Inn=80       Unn=10       Rmax=5.2m    gmin=52
.PARAM RRf=452m     Rrbond=33m   Rtb=9        g2=820m
.PARAM act=9.06

.FUNC   Pb(I,dT,Rb)  {Rb/(2*Rtb)*(I-limit((dT/(max(I,1n)*Rb)+RRf*I)*g2,0,I))**2}

X1  d1 g s Tj S3_75_g_var PARAMS: a={act} dVth={dVth} dR={dRdson} dgfs={dgfs} Inn={Inn} Unn={Unn} 
+                                         Rmax={Rmax} gmin={gmin} Rs={Rs} Rp={Rd} dC={dC} Rm={Rm}
Rg    g1     g    {Rg}   
Lg    gate   g1   {Lg*if(dgfs==99,0,1)}
Gs    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rm)}
Rsa   s1     s    1Meg
Ls    source s1   {Ls*if(dgfs==99,0,1)}
Rda   d1     d2   {Rd}
Ld    drain  d2   {Ld*if(dgfs==99,0,1)}
Rsb   source s1    10
Rga   gate   g1    10
Rdb   drain  d2    10

G_TH  0    Tb  VALUE =  {Pb(abs(I(Ls)),V(Tj,Tcase),Rrbond*(1+(limit((V(Tb)+V(Tj))/2,-200,999)-25)*4m))}
Cthb  Tb      0               6.05m
Rthb  Tb      Tj              {Rtb} 
Rth1  Tj      t1              {3.47m+limit(dZth,0,1)*1.28m}
Rth2  t1      t2              {39.17m+limit(dZth,0,1)*14.5m}
Rth3  t2      t3              {157.07m+limit(dZth,0,1)*48m}
Rth4  t3      t4              {196.71m+limit(dZth,0,1)*117.58m}
Rth5  t4      Tcase           {289.3m+limit(dZth,0,1)*172.92m}
Cth1  Tj      0               109.637u
Cth2  t1      0               307.558u
Cth3  t2      0               2.113m
Cth4  t3      0               1.53m
Cth5  t4      0               66.464m
Cth6  Tcase   0               190m

.ENDS

**********

.SUBCKT IPP80N07S4_05 drain gate source Tj Tcase PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 dZth=0 Ls=2n Ld=2.5n Lg=4n

.PARAM Rs=1.13m     Rg=2.2       Rd=350u      Rm=465u
.PARAM Inn=80       Unn=10       Rmax=5.2m    gmin=52
.PARAM RRf=452m     Rrbond=33m   Rtb=9        g2=820m
.PARAM act=9.06

.FUNC   Pb(I,dT,Rb)  {Rb/(2*Rtb)*(I-limit((dT/(max(I,1n)*Rb)+RRf*I)*g2,0,I))**2}

X1  d1 g s Tj S3_75_g_var PARAMS: a={act} dVth={dVth} dR={dRdson} dgfs={dgfs} Inn={Inn} Unn={Unn} 
+                                         Rmax={Rmax} gmin={gmin} Rs={Rs} Rp={Rd} dC={dC} Rm={Rm}
Rg    g1     g    {Rg}   
Lg    gate   g1   {Lg*if(dgfs==99,0,1)}
Gs    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rm)}
Rsa   s1     s    1Meg
Ls    source s1   {Ls*if(dgfs==99,0,1)}
Rda   d1     d2   {Rd}
Ld    drain  d2   {Ld*if(dgfs==99,0,1)}
Rsb   source s1    10
Rga   gate   g1    10
Rdb   drain  d2    10

G_TH  0    Tb  VALUE =  {Pb(abs(I(Ls)),V(Tj,Tcase),Rrbond*(1+(limit((V(Tb)+V(Tj))/2,-200,999)-25)*4m))}
Cthb  Tb      0               6.05m
Rthb  Tb      Tj              {Rtb} 
Rth1  Tj      t1              {3.47m+limit(dZth,0,1)*1.28m}
Rth2  t1      t2              {39.17m+limit(dZth,0,1)*14.5m}
Rth3  t2      t3              {157.07m+limit(dZth,0,1)*48m}
Rth4  t3      t4              {196.71m+limit(dZth,0,1)*117.58m}
Rth5  t4      Tcase           {289.3m+limit(dZth,0,1)*172.92m}
Cth1  Tj      0               109.637u
Cth2  t1      0               307.558u
Cth3  t2      0               2.113m
Cth4  t3      0               1.53m
Cth5  t4      0               66.464m
Cth6  Tcase   0               220m

.ENDS

**********

.SUBCKT IPI80N07S4_05 drain gate source Tj Tcase PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 dZth=0 Ls=2n Ld=2.5n Lg=4n

.PARAM Rs=1.13m     Rg=2.2       Rd=350u      Rm=465u
.PARAM Inn=80       Unn=10       Rmax=5.2m    gmin=52
.PARAM RRf=452m     Rrbond=33m   Rtb=9        g2=820m
.PARAM act=9.06

.FUNC   Pb(I,dT,Rb)  {Rb/(2*Rtb)*(I-limit((dT/(max(I,1n)*Rb)+RRf*I)*g2,0,I))**2}

X1  d1 g s Tj S3_75_g_var PARAMS: a={act} dVth={dVth} dR={dRdson} dgfs={dgfs} Inn={Inn} Unn={Unn} 
+                                         Rmax={Rmax} gmin={gmin} Rs={Rs} Rp={Rd} dC={dC} Rm={Rm}
Rg    g1     g    {Rg}   
Lg    gate   g1   {Lg*if(dgfs==99,0,1)}
Gs    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rm)}
Rsa   s1     s    1Meg
Ls    source s1   {Ls*if(dgfs==99,0,1)}
Rda   d1     d2   {Rd}
Ld    drain  d2   {Ld*if(dgfs==99,0,1)}
Rsb   source s1    10
Rga   gate   g1    10
Rdb   drain  d2    10

G_TH  0    Tb  VALUE =  {Pb(abs(I(Ls)),V(Tj,Tcase),Rrbond*(1+(limit((V(Tb)+V(Tj))/2,-200,999)-25)*4m))}
Cthb  Tb      0               6.05m
Rthb  Tb      Tj              {Rtb} 
Rth1  Tj      t1              {3.47m+limit(dZth,0,1)*1.28m}
Rth2  t1      t2              {39.17m+limit(dZth,0,1)*14.5m}
Rth3  t2      t3              {157.07m+limit(dZth,0,1)*48m}
Rth4  t3      t4              {196.71m+limit(dZth,0,1)*117.58m}
Rth5  t4      Tcase           {289.3m+limit(dZth,0,1)*172.92m}
Cth1  Tj      0               109.637u
Cth2  t1      0               307.558u
Cth3  t2      0               2.113m
Cth4  t3      0               1.53m
Cth5  t4      0               66.464m
Cth6  Tcase   0               190m

.ENDS



********************************************************************************
* Level 1 Models
********************************************************************************

.SUBCKT IPB80N07S4_05_L1 drain gate source PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 Ls=2n Ld=1n Lg=4n

.PARAM Rs=1.13m     Rg=2.2       Rd=50u       Rm=465u
.PARAM Inn=80       Unn=10       Rmax=5.2m    gmin=52
.PARAM act=9.06

X1  d1 g s Tj S3_75_g_var PARAMS: a={act} dVth={dVth} dR={dRdson} dgfs={dgfs} Inn={Inn} Unn={Unn} 
+                                         Rmax={Rmax} gmin={gmin} Rs={Rs} Rp={Rd} dC={dC} Rm={Rm}
Rg    g1     g    {Rg}   
Lg    gate   g1   {Lg*if(dgfs==99,0,1)}
Gs    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rm)}
Rsa   s1     s    1Meg
Ls    source s1   {Ls*if(dgfs==99,0,1)}
Rda   d1     d2   {Rd}
Ld    drain  d2   {Ld*if(dgfs==99,0,1)}

E1    Tj     w      VALUE={TEMP}
R1    w      0      1u

.ENDS

**********

.SUBCKT IPP80N07S4_05_L1 drain gate source PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 Ls=2n Ld=2.5n Lg=4n

.PARAM Rs=1.13m     Rg=2.2       Rd=350u      Rm=465u
.PARAM Inn=80       Unn=10       Rmax=5.2m    gmin=52
.PARAM act=9.06

X1  d1 g s Tj S3_75_g_var PARAMS: a={act} dVth={dVth} dR={dRdson} dgfs={dgfs} Inn={Inn} Unn={Unn} 
+                                         Rmax={Rmax} gmin={gmin} Rs={Rs} Rp={Rd} dC={dC} Rm={Rm}
Rg    g1     g    {Rg}   
Lg    gate   g1   {Lg*if(dgfs==99,0,1)}
Gs    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rm)}
Rsa   s1     s    1Meg
Ls    source s1   {Ls*if(dgfs==99,0,1)}
Rda   d1     d2   {Rd}
Ld    drain  d2   {Ld*if(dgfs==99,0,1)}

E1    Tj     w      VALUE={TEMP}
R1    w      0      1u

.ENDS

**********

.SUBCKT IPI80N07S4_05_L1 drain gate source PARAMS: dVth=0 dRdson=0 dgfs=0 dC=0 Ls=2n Ld=2.5n Lg=4n

.PARAM Rs=1.13m     Rg=2.2       Rd=350u      Rm=465u
.PARAM Inn=80       Unn=10       Rmax=5.2m    gmin=52
.PARAM act=9.06

X1  d1 g s Tj S3_75_g_var PARAMS: a={act} dVth={dVth} dR={dRdson} dgfs={dgfs} Inn={Inn} Unn={Unn} 
+                                         Rmax={Rmax} gmin={gmin} Rs={Rs} Rp={Rd} dC={dC} Rm={Rm}
Rg    g1     g    {Rg}   
Lg    gate   g1   {Lg*if(dgfs==99,0,1)}
Gs    s1     s    VALUE={V(s1,s)/(Rs*(1+(limit(V(Tj),-200,999)-25)*4m)-Rm)}
Rsa   s1     s    1Meg
Ls    source s1   {Ls*if(dgfs==99,0,1)}
Rda   d1     d2   {Rd}
Ld    drain  d2   {Ld*if(dgfs==99,0,1)}

E1    Tj     w      VALUE={TEMP}
R1    w      0      1u

.ENDS



********************************************************************************
* Level 0 Models
********************************************************************************

.SUBCKT IPB80N07S4_05_L0  drain  gate  source

Lg     gate  g1    4n
Ld     drain d1    1n
Ls     source s1   2n
Rs      s1    s2   1.13m

Rg     g1    g2     2.2
M1      d2    g2    s2    s2    DMOS    L=1u   W=1u
.MODEL DMOS NMOS ( KP= 160.4  VTO=4.2  THETA=0  VMAX=1.5e5  ETA=0.006  LEVEL=3)
Rd     d1    d2    2.11m TC=7m

Dbd     s2    d2    Dbt
.MODEL     Dbt    D(BV=88   M=0.27  CJO=2.58n  VJ=0.9V)
Dbody   s2   21    DBODY
.MODEL DBODY  D(IS=68.9p  N=1.16  RS=0.06u  EG=1.12  TT=40n)
Rdiode  d1  21    0.98m TC=5m

.MODEL   sw    NMOS(VTO=0  KP=10   LEVEL=1)
Maux      g2   c    a    a   sw
Maux2     b    d    g2    g2   sw
Eaux      c    a    d2    g2   1
Eaux2     d    g2   d2    g2   -1
Cox       b    d2   1.14n
.MODEL     DGD    D(M=0.7   CJO=1.14n   VJ=0.5)
Rpar      b    d2   1Meg
Dgd       a    d2   DGD
Rpar2     d2   a    10Meg
Cgs     g2    s2    3.53n

.ENDS  IPB80N07S4_05_L0

******

.SUBCKT IPP80N07S4_05_L0  drain  gate  source

Lg     gate  g1    4n
Ld     drain d1    2.5n
Ls     source s1   2n
Rs      s1    s2   1.13m

Rg     g1    g2     2.2
M1      d2    g2    s2    s2    DMOS    L=1u   W=1u
.MODEL DMOS NMOS ( KP= 160.4  VTO=4.2  THETA=0  VMAX=1.5e5  ETA=0.006  LEVEL=3)
Rd     d1    d2    2.41m TC=7m

Dbd     s2    d2    Dbt
.MODEL     Dbt    D(BV=88   M=0.27  CJO=2.58n  VJ=0.9V)
Dbody   s2   21    DBODY
.MODEL DBODY  D(IS=68.9p  N=1.16  RS=0.06u  EG=1.12  TT=40n)
Rdiode  d1  21    0.98m TC=5m

.MODEL   sw    NMOS(VTO=0  KP=10   LEVEL=1)
Maux      g2   c    a    a   sw
Maux2     b    d    g2    g2   sw
Eaux      c    a    d2    g2   1
Eaux2     d    g2   d2    g2   -1
Cox       b    d2   1.14n
.MODEL     DGD    D(M=0.7   CJO=1.14n   VJ=0.5)
Rpar      b    d2   1Meg
Dgd       a    d2   DGD
Rpar2     d2   a    10Meg
Cgs     g2    s2    3.53n

.ENDS  IPP80N07S4_05_L0

******

.SUBCKT IPI80N07S4_05_L0  drain  gate  source

Lg     gate  g1    4n
Ld     drain d1    2.5n
Ls     source s1   2n
Rs      s1    s2   1.13m

Rg     g1    g2     2.2
M1      d2    g2    s2    s2    DMOS    L=1u   W=1u
.MODEL DMOS NMOS ( KP= 160.4  VTO=4.2  THETA=0  VMAX=1.5e5  ETA=0.006  LEVEL=3)
Rd     d1    d2    2.41m TC=7m

Dbd     s2    d2    Dbt
.MODEL     Dbt    D(BV=88   M=0.27  CJO=2.58n  VJ=0.9V)
Dbody   s2   21    DBODY
.MODEL DBODY  D(IS=68.9p  N=1.16  RS=0.06u  EG=1.12  TT=40n)
Rdiode  d1  21    0.98m TC=5m

.MODEL   sw    NMOS(VTO=0  KP=10   LEVEL=1)
Maux      g2   c    a    a   sw
Maux2     b    d    g2    g2   sw
Eaux      c    a    d2    g2   1
Eaux2     d    g2   d2    g2   -1
Cox       b    d2   1.14n
.MODEL     DGD    D(M=0.7   CJO=1.14n   VJ=0.5)
Rpar      b    d2   1Meg
Dgd       a    d2   DGD
Rpar2     d2   a    10Meg
Cgs     g2    s2    3.53n

.ENDS  IPI80N07S4_05_L0

**********
